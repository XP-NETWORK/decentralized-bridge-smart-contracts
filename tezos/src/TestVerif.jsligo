#import "./Bridge.jsligo" "Bridge"

#import "./StorageFactory.jsligo" "StorageFactory"

#import "./CollectionFactory.jsligo" "CollectionFactory"

#import "./NFT.jsligo" "NFT"

const bootstrap_validator = "tz1NqGupPkw59EHtc6K1YfMqrmgHAjz4qWoB" as address;
const bootstrap_validato_key = "edpktxsd1GCJAsKXmtJX6pUKNoHSvkC2NqxnojNacKL5gYpxufJp3x" as key;

export type AddrOrStr = ["Addr", address] | ["Str", string];

const deploy_storage_deployer = (): typed_address<
    parameter_of StorageFactory.StorageFactory,
    StorageFactory.StorageFactory.storage
> => {
    let initial_storage: StorageFactory.StorageFactory.storage = {
        owner: None(),
        collection_to_store: Map.empty,
    }
    let { addr, code, size } =
        Test.originate(
            contract_of(StorageFactory.StorageFactory),
            initial_storage,
            0mutez
        );
    return addr;
};

const deploy_collection_deployer = (): typed_address<
    parameter_of CollectionFactory.CollectionFactory,
    CollectionFactory.CollectionFactory.storage
> => {
    let initial_storage: CollectionFactory.CollectionFactory.storage = {
        owner: None(),
        collection_to_store: Map.empty,
    }
    let { addr, code, size } =
        Test.originate(
            contract_of(CollectionFactory.CollectionFactory),
            initial_storage,
            0mutez
        );
    return addr;
};

const deploy_nft_contract = (): typed_address<
    parameter_of NFT.NFT,
    NFT.NFT.storage
> => {
    let [admin, _b, _a] = Test.get_bootstrap_account(1n);
    let initial_storage: NFT.NFT.storage = {
        ledger: Big_map.empty,
        operators: Big_map.empty,
        token_metadata: Big_map.empty,
        metadata: Big_map.empty,
        admin,
    }
    let { addr, code, size } =
        Test.originate(contract_of(NFT.NFT), initial_storage, 0mutez);
    return addr;
};

const deploy_bridge_contracts = (cd: address, sd: address): typed_address<
    parameter_of Bridge.Bridge,
    Bridge.Bridge.storage
> => {
    const initial_storage: Bridge.Bridge.storage = {
        validators: Big_map.literal(list([[bootstrap_validator, 0mutez]])),
        unique_identifiers: Big_map.empty,
        collection_deployer: cd,
        storage_deployer: sd,
        original_to_duplicate_mapping: Big_map.empty,
        duplicate_to_original_mapping: Big_map.empty,
        original_storage_mapping_nft: Big_map.empty,
        original_storage_mapping_sft: Big_map.empty,
        duplicate_storage_mapping_nft: Big_map.empty,
        duplicate_storage_mapping_sft: Big_map.empty,
        validators_count: 1n,
    };
    let { addr, code, size } =
        Test.originate(contract_of(Bridge.Bridge), initial_storage, 0mutez);
    return addr;
};

function setup(): typed_address<
    parameter_of Bridge.Bridge,
    Bridge.Bridge.storage
> {
    let storage_deployer = deploy_storage_deployer();
    let collection_deployer = deploy_collection_deployer();
    let bridge =
        deploy_bridge_contracts(
            Test.to_address(collection_deployer),
            Test.to_address(storage_deployer)
        );
    Test.print("Storage Deployer: ");
    Test.println(Test.to_string(storage_deployer));
    Test.print("Collection Deployer: ");
    Test.println(Test.to_string(collection_deployer));
    Test.print("Bridge: ");
    Test.println(Test.to_string(bridge));
    let pc: parameter_of CollectionFactory.CollectionFactory =
        Set_owner({ new_owner: Test.to_address(bridge) });
    let ps: parameter_of StorageFactory.StorageFactory =
        Set_owner({ new_owner: Test.to_address(bridge) });
    let tfc = Test.transfer_exn(collection_deployer, pc, 0mutez);
    let tfs = Test.transfer_exn(storage_deployer, ps, 0mutez);
    assert(
        Test.get_storage(collection_deployer).owner
        == Some(Test.to_address(bridge))
    );
    assert(
        Test.get_storage(storage_deployer).owner
        == Some(Test.to_address(bridge))
    );
    return bridge;
};

function claim_nft() {
    let bridge = setup();
    let cd: parameter_of Bridge.Bridge =
        Claim_nft(
            {
                data: {
                    token_id: 25n,
                    source_chain: "BSC",
                    dest_chain: "TEZOS",
                    dest_address: "tz1hmsQEAzt7F1y7X6xjv1U4pqk4xeKKKPcR" as
                        address,
                    source_nft_contract_address: Str(
                        "0xa702b3873c6818de60d1495b792eefd1e4ebb2a2"
                    ),
                    name: "DUM",
                    symbol: "D",
                    royalty: 0n,
                    royalty_receiver: "tz1NqGupPkw59EHtc6K1YfMqrmgHAjz4qWoB" as
                        address,
                    metadata: "https://ipfs.io/ipfs/QmToNVaaxKDPZnJmBXXCcFXbTyBmVzmKtoRcQBzsfTNbUT",
                    transaction_hash: "0xfc20f11690da4c77dbd9c2ab243c3c66d489ffb3f972646cfe88d237167a36fa",
                    token_amount: 1n,
                    nft_type: "singular",
                    fee: 10000mutez
                },
                sigs: list(
                    [
                        {
                            addr: "tz1NqGupPkw59EHtc6K1YfMqrmgHAjz4qWoB" as
                                address,
                            sig: "sigXHDYECuD58kiYsvfMCyre6Vw7wZkXhWXQbNsW6kcMEkeEVHNQFizZz5xdgm47NdqXDjKj6Bar3XYPT8ZNuovXwXDaxHLQ" as
                                signature,
                            signer: "edpktxsd1GCJAsKXmtJX6pUKNoHSvkC2NqxnojNacKL5gYpxufJp3x" as
                                key
                        }
                    ]
                );
            }
        )
    let tfc = Test.transfer_exn(bridge, cd, 10000mutez);
    // Test.println(Test.to_json(Test.get_storage(bridge)));
    const events: list<{ source_chain: string, tx_hash: string }> =
        Test.get_last_events_from(bridge, "claimed");
    Test.println(Test.to_string(events));
};

function lock_nft(): unit {
    const nft = deploy_nft_contract();
    const pc: parameter_of NFT.NFT =
        Mint(
            list(
                [
                    {
                        token_uri: "https://ipfs.io/ipfs/QmToNVaaxKDPZnJmBXXCcFXbTyBmVzmKtoRcQBzsfTNbUT",
                        amt: 1n,
                        token_id: 1n,
                        to: Test.nth_bootstrap_account(1),
                    },
                    {
                        token_uri: "https://ipfs.io/ipfs/QmToNVaaxKDPZnJmBXXCcFXbTyBmVzmKtoRcQBzsfTNbUT",
                        amt: 1n,
                        token_id: 2n,
                        to: Test.nth_bootstrap_account(1),
                    }
                ]
            )
        );
    const bridge = setup();
    const approve: parameter_of NFT.NFT =
        Update_operators(
            list(
                [
                    Add_operator(
                        {
                            owner: Test.nth_bootstrap_account(1),
                            operator: Test.to_address(bridge),
                            token_id: 1n
                        }
                    )
                ]
            )
        );
    const approve2: parameter_of NFT.NFT =
        Update_operators(
            list(
                [
                    Add_operator(
                        {
                            owner: Test.nth_bootstrap_account(1),
                            operator: Test.to_address(bridge),
                            token_id: 2n
                        }
                    )
                ]
            )
        );
    const tfc = Test.transfer_exn(nft, pc, 0mutez);
    const tfa = Test.transfer_exn(nft, approve, 0mutez);
    const tfa2 = Test.transfer_exn(nft, approve2, 0mutez);
    const pl: parameter_of Bridge.Bridge =
        Lock_nft(
            {
                token_id: 1n,
                dest_chain: "BSC",
                dest_address: "BSC ADDR",
                source_nft_address: Addr(Test.to_address(nft))
            }
        );
    const pl2: parameter_of Bridge.Bridge =
        Lock_nft(
            {
                token_id: 2n,
                dest_chain: "BSC",
                dest_address: "BSC ADDR",
                source_nft_address: Addr(Test.to_address(nft))
            }
        );
    const tfl = Test.transfer_exn(bridge, pl, 0mutez);
    const tfl2 = Test.transfer_exn(bridge, pl2, 0mutez);
    const events: list<
        {
            token_id: nat,
            dest_chain: string,
            dest_address: string,
            source_nft_address: address,
            token_amount: nat,
            nft_type: string,
            source_chain: string
        }
    > =
        Test.get_last_events_from(bridge, "locked");
    Test.println(Test.to_string(events));
}

function add_validator(): unit {
    const bridge = setup();
    let [admin, key, sig] = Test.get_bootstrap_account(1n);
        const add_p: parameter_of Bridge.Bridge =
        Add_validator(
            {
                sigs: list([
                   { addr: admin, sig: "sigjYbYHRyfbBF5WK8MS8CRE8zxqXhHgA2Fd8CXpqJ3foeHiK7yhzgM1wHMNtgJwJa27GgeGoxo7LcP5Y1dKbwrr821nH9DK" as signature, signer: bootstrap_validato_key},
                ]),
                validator: "tz1PPSVMDyKjqEPQaJfNfwbU2TKt6ywoU2UP" as address
            }
        );
    const tfc = Test.transfer_exn(bridge, add_p, 0mutez);
    const events: list<{ validator: address }> =
        Test.get_last_events_from(bridge, "added_new_validator");
        assert(List.length(events) > 0n );
        Test.println(Test.to_string(events));
}

const _ = claim_nft();

const _a = lock_nft();

const _b = add_validator();
